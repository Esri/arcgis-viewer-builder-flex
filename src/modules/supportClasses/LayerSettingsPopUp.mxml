<?xml version="1.0" encoding="utf-8"?>
<!--
  //////////////////////////////////////////////////////////////////
  // Copyright (c) 2008-2013 Esri. All Rights Reserved.
  //
  // Licensed under the Apache License, Version 2.0 (the "License");
  // you may not use this file except in compliance with the License.
  // You may obtain a copy of the License at
  //
  //    http://www.apache.org/licenses/LICENSE-2.0
  //
  // Unless required by applicable law or agreed to in writing, software
  // distributed under the License is distributed on an "AS IS" BASIS,
  // WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  // See the License for the specific language governing permissions and
  // limitations under the License.
  ////////////////////////////////////////////////////////////////
-->
<s:SkinnablePopUpContainer xmlns:fx="http://ns.adobe.com/mxml/2009"
                           xmlns:s="library://ns.adobe.com/flex/spark"
                           xmlns:components="com.esri.builder.components.*"
                           width="100%"
                           creationComplete="this_creationCompleteHandler(event)"
                           minHeight="85"
                           minWidth="250"
                           stateChangeComplete="updatePopUpPosition()">
    <fx:Script>
        <![CDATA[
            import com.esri.ags.layers.supportClasses.EditFieldsInfo;
            import com.esri.ags.layers.supportClasses.FeatureLayerDetails;
            import com.esri.ags.layers.supportClasses.Field;
            import com.esri.ags.layers.supportClasses.LayerDetails;
            import com.esri.ags.portal.supportClasses.PopUpFieldFormat;
            import com.esri.builder.components.ModifyItemEvent;
            import com.esri.builder.renderers.MovableEditableGridItemRenderer;
            import com.esri.builder.renderers.MovableGridItemRenderer;
            import com.esri.builder.supportClasses.FieldUtil;
            import com.esri.builder.views.popups.FieldFormatPopUp;

            import mx.collections.ArrayList;
            import mx.core.FlexGlobals;
            import mx.events.CollectionEvent;
            import mx.events.CollectionEventKind;
            import mx.events.FlexEvent;
            import mx.managers.PopUpManager;

            import spark.events.PopUpEvent;

            [Bindable]
            public var showAttachmentsEnabled:Boolean;
            [Bindable]
            public var showRelatedRecordsEnabled:Boolean;

            [Bindable]
            public var supportsAttachments:Boolean;
            [Bindable]
            public var supportsRelatedRecords:Boolean;

            [Bindable]
            private var showRelatedRecords:Boolean;
            [Bindable]
            private var showAttachments:Boolean;

            private var availableFields:Array;

            public var originalLayerSettings:LayerSettings;

            private var _layerDetails:LayerDetails;

            public function get layerDetails():LayerDetails
            {
                return _layerDetails;
            }

            public function set layerDetails(value:LayerDetails):void
            {
                if (value)
                {
                    _layerDetails = value;
                    setOptions(value);
                }
            }

            private var _showRequired:Boolean = false;

            public function get showRequired():Boolean
            {
                return _showRequired;
            }

            public function set showRequired(value:Boolean):void
            {
                _showRequired = value;
                currentState = _showRequired ? "showRequired" : "normal"
            }

            public var layerName:String;

            [Bindable]
            private var availableFieldItems:ArrayList = new ArrayList();

            protected function this_creationCompleteHandler(event:FlexEvent):void
            {
                availableFieldItems.addEventListener(CollectionEvent.COLLECTION_CHANGE, availableItems_collectionChangeHandler);
            }

            protected function availableItems_collectionChangeHandler(event:CollectionEvent):void
            {
                if (event.kind == CollectionEventKind.UPDATE
                    || event.kind == CollectionEventKind.REFRESH
                    || event.kind == CollectionEventKind.RESET)
                {
                    dispatchEvent(new Event("itemInclusionChanged"));
                }
            }

            [Bindable(event="itemInclusionChanged")]
            private function get hasValidLayerSettingsOptions():Boolean
            {
                return hasAtLeastOneSelectedItem(availableFieldItems.source);
            }

            private function hasAtLeastOneSelectedItem(items:Array):Boolean
            {
                for each (var item:Object in items)
                {
                    if (item.selected === true)
                    {
                        return true;
                    }
                }

                return false;
            }

            public function overrideLayerSettings(layerSettings:LayerSettings):void
            {
                originalLayerSettings = layerSettings;
            }

            private function setOptions(layerDetails:LayerDetails):void
            {
                if (!layerDetails)
                {
                    return;
                }

                var fieldNameToUserDefinedFieldSettings:Dictionary = new Dictionary();
                if (originalLayerSettings)
                {
                    for each (var fieldSettings:FieldSettings in originalLayerSettings.fields)
                    {
                        fieldNameToUserDefinedFieldSettings[fieldSettings.name] = fieldSettings;
                    }

                    showAttachments = originalLayerSettings.showAttachments;
                    showRelatedRecords = originalLayerSettings.showRelatedRecords;
                }

                var serverVersion:Number = layerDetails.version;
                var isRequiredEditable:Boolean;

                var isEditorTrackingField:Boolean;
                var editorTrackingFieldNames:Array = getEditorTrackingFieldNames(layerDetails);

                availableFields = FieldUtil.getValidFields(layerDetails.fields);
                var atLeastOneFieldMatched:Boolean;

                var fieldNameToFieldSettingsOption:Dictionary = new Dictionary();

                for each (var field:Field in availableFields)
                {
                    isEditorTrackingField = editorTrackingFieldNames.indexOf(field.name) > -1;
                    isRequiredEditable = serverVersion < 10.1 || field.nullable;

                    var fieldSettings:FieldSettings = new FieldSettings();
                    var originalFieldSettings:FieldSettings = fieldNameToUserDefinedFieldSettings[field.name];

                    if (originalFieldSettings)
                    {
                        atLeastOneFieldMatched = true;

                        fieldSettings.name = originalFieldSettings.name;
                        fieldSettings.alias = originalFieldSettings.alias;
                        fieldSettings.tooltip = originalFieldSettings.tooltip;
                        fieldSettings.dateFormat = originalFieldSettings.dateFormat;
                        fieldSettings.useUTC = originalFieldSettings.useUTC;
                        if (isRequiredEditable && !isEditorTrackingField)
                        {
                            fieldSettings.required = originalFieldSettings.required;
                        }
                    }
                    else
                    {
                        fieldSettings.name = field.name;
                        fieldSettings.alias = field.alias;
                        fieldSettings.required = !isRequiredEditable;
                    }

                    fieldNameToFieldSettingsOption[field.name] =
                        {
                            settings: fieldSettings,
                            isRequiredEditable: isRequiredEditable,
                            isEditorTrackingField: isEditorTrackingField,
                            selected: true,
                            editable: field.editable
                        };
                }

                var fieldSettingsOptions:Array = [];
                if (originalLayerSettings)
                {
                    for each (var fieldSettings:FieldSettings in originalLayerSettings.fields)
                    {
                        fieldSettingsOptions.push(fieldNameToFieldSettingsOption[fieldSettings.name]);
                    }
                }

                var fieldSettingsOption:Object;
                for each (var field:Field in availableFields)
                {
                    fieldSettingsOption = fieldNameToFieldSettingsOption[field.name];

                    if (atLeastOneFieldMatched)
                    {
                        fieldSettingsOption.selected = fieldNameToUserDefinedFieldSettings[field.name] != null;
                    }

                    if (!fieldNameToUserDefinedFieldSettings[field.name])
                    {
                        fieldSettingsOptions.push(fieldSettingsOption);
                    }
                }

                supportsAttachments = layerDetails.hasAttachments;
                supportsRelatedRecords = layerDetails.relationships
                    && layerDetails.relationships.length > 0;

                availableFieldItems.source = fieldSettingsOptions;
                updatePopUpPosition();
            }

            private function resetOptions():void
            {
                availableFieldItems.removeAll();
                updatePopUpPosition();
            }

            private function commitAndClose():void
            {
                var layerSettings:LayerSettings = new LayerSettings();
                layerSettings.name = layerName;
                layerSettings.fields = getIncludedFieldSettings();
                layerSettings.showAttachments = showAttachments;
                layerSettings.showRelatedRecords = showRelatedRecords;

                close(true, { originalLayerSettings: originalLayerSettings, layerSettings: layerSettings });
            }

            override public function close(commit:Boolean = false, data:* = null):void
            {
                availableFieldItems.removeEventListener(CollectionEvent.COLLECTION_CHANGE, availableItems_collectionChangeHandler);
                super.close(commit, data);
            }

            override public function updatePopUpPosition():void
            {
                PopUpManager.centerPopUp(this);
            }

            private function getIncludedFieldSettings():Array
            {
                var allFieldSettings:Array = [];

                var fieldItems:Array = availableFieldItems.source;
                for each (var fieldItem:Object in fieldItems)
                {
                    if (fieldItem.selected)
                    {
                        allFieldSettings.push(fieldItem.settings);
                    }
                }

                return allFieldSettings;
            }

            private function getEditorTrackingFieldNames(layerDetails:LayerDetails):Array
            {
                var editorTrackingFieldNames:Array = [];

                var editFieldsInfo:EditFieldsInfo;

                var featureLayerDetails:FeatureLayerDetails = layerDetails as FeatureLayerDetails;
                if (featureLayerDetails)
                {
                    editFieldsInfo = featureLayerDetails.editFieldsInfo;
                }

                if (editFieldsInfo)
                {
                    editorTrackingFieldNames.push(editFieldsInfo.creatorField);
                    editorTrackingFieldNames.push(editFieldsInfo.creationDateField);
                    editorTrackingFieldNames.push(editFieldsInfo.editorField);
                    editorTrackingFieldNames.push(editFieldsInfo.editDateField);
                }

                return editorTrackingFieldNames;
            }

            private function actionsItemRenderer(fieldItem:Object, column:GridColumn):IFactory
            {
                var isDateField:Boolean;

                var field:Field = findMatchingField(fieldItem.settings.name);
                isDateField = field && (field.type == Field.TYPE_DATE);

                var itemRendererClass:Class =
                    (isDateField) ? MovableEditableGridItemRenderer : MovableGridItemRenderer;

                return new ClassFactory(itemRendererClass);
            }

            private function findMatchingField(name:String):Field
            {
                var matchingField:Field;

                for each (var field:Field in availableFields)
                {
                    if (field.name == name)
                    {
                        matchingField = field;
                        break;
                    }
                }

                return matchingField;
            }

            private function fieldItemsDataGrid_editItemHandler(event:ModifyItemEvent):void
            {
                var fieldFormatPopUp:FieldFormatPopUp = new FieldFormatPopUp();
                var fieldItem:Object = event.item;
                var fieldName:String = fieldItem.settings.name;

                var fieldSettings:FieldSettings = findMatchingFieldSettings(fieldName);
                if (fieldSettings)
                {
                    //we create a fake field format
                    //since it's not a part of FieldSettings
                    //but required by FieldFormatPopUp
                    var fieldFormat:PopUpFieldFormat = new PopUpFieldFormat();
                    fieldFormat.dateFormat = fieldSettings.dateFormat;
                    fieldFormat.useUTC = fieldSettings.useUTC;
                    fieldFormatPopUp.overrideFieldFormat(fieldFormat);
                }

                fieldFormatPopUp.field = findMatchingField(fieldName);
                fieldFormatPopUp.addEventListener(PopUpEvent.CLOSE, fieldFormatPopUp_closeHandler);
                fieldFormatPopUp.open(FlexGlobals.topLevelApplication as DisplayObjectContainer, true);
            }

            private function findMatchingFieldSettings(fieldName:String):FieldSettings
            {
                var settings:FieldSettings;

                for each (var fieldItem:Object in availableFieldItems.source)
                {
                    if (fieldItem.settings.name == fieldName)
                    {
                        settings = fieldItem.settings;
                        break;
                    }
                }

                return settings;
            }

            private function fieldFormatPopUp_closeHandler(event:PopUpEvent):void
            {
                var fieldFormatPopUp:FieldFormatPopUp = event.currentTarget as FieldFormatPopUp;
                fieldFormatPopUp.removeEventListener(PopUpEvent.CLOSE, fieldFormatPopUp_closeHandler);
                if (event.commit)
                {
                    var fieldFormat:PopUpFieldFormat = event.data.fieldFormat as PopUpFieldFormat;
                    var fieldSettings:FieldSettings = findMatchingFieldSettings(fieldFormatPopUp.field.name);
                    fieldSettings.dateFormat = fieldFormat.dateFormat;
                    fieldSettings.useUTC = fieldFormat.useUTC;
                }
            }
        ]]>
    </fx:Script>

    <s:states>
        <s:State name="normal"/>
        <s:State name="showRequired"/>
    </s:states>

    <!--background-->
    <s:Rect left="0" right="0" top="0" bottom="0">
        <s:fill>
            <s:SolidColor color="#FFFFFF"/>
        </s:fill>
        <s:stroke>
            <s:SolidColorStroke caps="none"
                                color="#000000"
                                joints="miter"
                                miterLimit="4"
                                weight="2"/>
        </s:stroke>
    </s:Rect>

    <s:Rect height="25"
            left="2" right="2" top="2">
        <s:fill>
            <s:LinearGradient rotation="90">
                <s:GradientEntry alpha="1"
                                 color="#FFFFFF"
                                 ratio="0"/>
                <s:GradientEntry alpha="1.0"
                                 color="#CCCCCC"
                                 ratio="1"/>
            </s:LinearGradient>
        </s:fill>
    </s:Rect>

    <s:Line left="0" right="0" top="26">
        <s:stroke>
            <s:SolidColorStroke caps="none"
                                color="#000000"
                                joints="miter"
                                miterLimit="4"
                                weight="2"/>
        </s:stroke>
    </s:Line>

    <!--content-->
    <s:Label left="10" top="9"
             color="#000000"
             fontSize="14"
             styleName="boldText"
             text="{resourceManager.getString('BuilderStrings', 'configureLayerFields')}"
             textAlign="center"
             verticalAlign="middle"/>

    <s:Form width="100%" top="20">
        <s:layout>
            <s:FormLayout gap="-10"/>
        </s:layout>

        <s:FormItem width="100%" label="{resourceManager.getString('BuilderStrings', 'edit.fields')}">
            <s:Label includeInLayout="{availableFieldItems.length == 0}"
                     text="{resourceManager.getString('BuilderStrings', 'noFieldsAvailable')}"
                     visible="{availableFieldItems.length == 0}"/>

            <components:ItemModifierDataGrid width="100%" height="250"
                                             dataProvider="{availableFieldItems}"
                                             doubleClickEnabled="false"
                                             editItem="fieldItemsDataGrid_editItemHandler(event)"
                                             editable="true"
                                             includeInLayout="{availableFieldItems.length &gt; 0}"
                                             visible="{availableFieldItems.length &gt; 0}">
                <components:columns>
                    <s:ArrayList>
                        <s:GridColumn width="60"
                                      dataField="selected"
                                      editable="false"
                                      headerRenderer="com.esri.builder.renderers.CheckBoxGridHeaderRenderer"
                                      headerText="{resourceManager.getString('BuilderStrings', 'include')}"
                                      itemRenderer="com.esri.builder.renderers.CheckBoxGridItemRenderer"
                                      maxWidth="60"/>

                        <s:GridColumn width="100"
                                      dataField="settings.name"
                                      headerText="{resourceManager.getString('BuilderStrings', 'displayFields.name')}"
                                      maxWidth="200"/>

                        <s:GridColumn width="100"
                                      dataField="settings.alias"
                                      editable="true"
                                      headerText="{resourceManager.getString('BuilderStrings', 'displayFields.alias')}"
                                      maxWidth="200"/>

                        <s:GridColumn width="100"
                                      dataField="settings.required"
                                      editable="false"
                                      headerText="{resourceManager.getString('BuilderStrings', 'geoprocessing.required')}"
                                      includeIn="showRequired"
                                      maxWidth="200">
                            <s:itemRenderer>
                                <fx:Component>
                                    <s:GridItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009"
                                                        xmlns:s="library://ns.adobe.com/flex/spark"
                                                        clipAndEnableScrolling="true">
                                        <fx:Script>
                                            <![CDATA[
                                                [Bindable]
                                                private var isFieldVisible:Boolean;
                                                [Bindable]
                                                private var isRequiredEditable:Boolean;
                                                [Bindable]
                                                private var isEditable:Boolean;
                                                [Bindable]
                                                private var isEditorTrackingField:Boolean;

                                                override public function prepare(hasBeenRecycled:Boolean):void
                                                {
                                                    if (data)
                                                    {
                                                        isEditorTrackingField = data["isEditorTrackingField"];
                                                        isEditable = data["editable"];
                                                        isFieldVisible = data["selected"];
                                                        isRequiredEditable = data["isRequiredEditable"];

                                                        if (isFieldVisible)
                                                        {
                                                            checkBox.selected = isRequiredEditable ? data[column.dataField] : true;
                                                        }
                                                        else
                                                        {
                                                            checkBox.selected = false;
                                                        }
                                                    }
                                                }

                                                private function updateDataField():void
                                                {
                                                    var isSelected:Boolean = checkBox.selected;
                                                    data[column.dataField] = isSelected;
                                                    grid.dataProvider.itemUpdated(data, column.dataField, !isSelected, isSelected);
                                                }
                                            ]]>
                                        </fx:Script>

                                        <s:CheckBox id="checkBox"
                                                    change="updateDataField()"
                                                    enabled="{isRequiredEditable &amp;&amp; isFieldVisible}"
                                                    horizontalCenter="0"
                                                    verticalCenter="0"
                                                    visible="{isEditable &amp;&amp; !isEditorTrackingField}"/>
                                    </s:GridItemRenderer>
                                </fx:Component>
                            </s:itemRenderer>
                        </s:GridColumn>

                        <s:GridColumn width="100"
                                      editable="false"
                                      headerText="{resourceManager.getString('BuilderStrings', 'popUpConfigPopUp.media.actions')}"
                                      itemRendererFunction="actionsItemRenderer"/>
                    </s:ArrayList>
                </components:columns>
            </components:ItemModifierDataGrid>
        </s:FormItem>

        <s:FormItem includeInLayout="{showAttachmentsEnabled &amp;&amp; supportsAttachments}"
                    label="{resourceManager.getString('BuilderStrings', 'popUpConfigPopUp.showAttachments')}"
                    visible="{showAttachmentsEnabled &amp;&amp; supportsAttachments}">
            <components:SliderToggleButton deselectedLabel="{resourceManager.getString('BuilderStrings', 'off')}"
                                           selected="@{showAttachments}"
                                           selectedLabel="{resourceManager.getString('BuilderStrings', 'on')}"/>
        </s:FormItem>

        <s:FormItem includeInLayout="{showRelatedRecordsEnabled &amp;&amp; supportsRelatedRecords}"
                    label="{resourceManager.getString('BuilderStrings', 'popUpConfigPopUp.showRelatedRecords')}"
                    visible="{showRelatedRecordsEnabled &amp;&amp; supportsRelatedRecords}">
            <components:SliderToggleButton deselectedLabel="{resourceManager.getString('BuilderStrings', 'off')}"
                                           selected="@{showRelatedRecords}"
                                           selectedLabel="{resourceManager.getString('BuilderStrings', 'on')}"/>
        </s:FormItem>

        <s:FormItem>
            <s:layout>
                <s:HorizontalLayout horizontalAlign="center" verticalAlign="middle"/>
            </s:layout>

            <s:Button click="commitAndClose()"
                      enabled="{hasValidLayerSettingsOptions}"
                      label="{resourceManager.getString('BuilderStrings', 'ok')}"
                      skinClass="com.esri.builder.skins.BlueButtonSkin"/>

            <s:Button click="close(false)"
                      label="{resourceManager.getString('BuilderStrings', 'cancel')}"
                      skinClass="com.esri.builder.skins.LinkButtonSkin"/>
        </s:FormItem>
    </s:Form>
</s:SkinnablePopUpContainer>
